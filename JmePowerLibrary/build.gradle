// Note: "common.gradle" in the root project contains additional initialization
//   for this project. This initialization is applied in the "build.gradle"
//   of the root project.

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

description = 'a startup screen for jMonkeyEngine'
ext {
    artifact = 'JmePower'
    metadata = jmeTarget

    if (metadata.equals('')) {
        version = jmePowerVersion
    } else {
        version = "${jmePowerVersion}+${metadata}"
    }
    baseName = "${artifact}-${version}"

    resourcesDir = 'src/main/resources/'
}

jar { archiveBaseName = project.ext.baseName }
javadocJar { archiveBaseName = project.ext.baseName }
sourcesJar { archiveBaseName = project.ext.baseName }

dependencies {
    api 'org.jmonkeyengine:jme3-core:' + jmonkeyengineVersion
    api 'com.github.stephengold:Heart:' + heartVersion
}

processResources.dependsOn(':JmePowerAssets:poweredBy')

build.dependsOn('pom', 'pomAsc')
task('pom', type: Copy) {
    dependsOn 'generatePomFileForMavenPublication'
    from "${buildDir}/publications/maven/pom-default.xml"
    into "${buildDir}/libs"
    rename 'pom-default.xml', baseName + '.pom'
}
task('pomAsc', type: Copy) {
    dependsOn 'signMavenPublication'
    from "${buildDir}/publications/maven/pom-default.xml.asc"
    into "${buildDir}/libs"
    rename 'pom-default.xml.asc', baseName + '.pom.asc'
}

publishToMavenLocal.doLast {
    println 'published to ' + baseName
}

publishing {
    publications {
        maven(MavenPublication) {
            artifact javadocJar
            artifact sourcesJar
            artifactId artifact
            from components.java
            pom {
                description = project.description
                developers {
                    developer {
                        email = 'sgold@sonic.net'
                        name = 'Stephen Gold'
                    }
                }
                licenses {
                    license {
                        distribution = 'repo'
                        name = 'New BSD (3-clause) License'
                        url = 'https://opensource.org/licenses/BSD-3-Clause'
                    }
                }
                name = 'com.github.stephengold:' + artifact
                scm {
                    connection = 'scm:git:git://github.com/stephengold/JmePower.git'
                    developerConnection = 'scm:git:ssh://github.com:stephengold/JmePower.git'
                    url = 'https://github.com/stephengold/JmePower/tree/master'
                }
                url = 'https://github.com/stephengold/JmePower'
            }
            version project.ext.version
        }
    }
}

signing {
    sign configurations.archives
    sign publishing.publications.maven
}
tasks.withType(Sign) {
    onlyIf { gradle.rootProject.hasProperty('signing.keyId') }
}
