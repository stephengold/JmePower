// Gradle script to build the JmePowerAssets subproject of JmePower

// Note: "common.gradle" in the root project contains additional initialization
//   for this project. This initialization is applied in the "build.gradle"
//   of the root project.

ext {
    // output directories
    assets = '../JmePowerLibrary/src/main/resources/'

    // generated texture files
    poweredByTex = assets + 'Textures/JmePower/powered-by.jpeg'
}

dependencies {
    implementation(libs.heart)
    implementation(libs.imgscalr)
}

tasks.register('textures') {
    dependsOn 'poweredBy'
    description 'Generates texture assets used in JmePower.'
}
tasks.register('poweredBy', JavaExec) {
    mainClass = 'com.github.stephengold.jmepower.assets.MakePoweredBy'
    outputs.files([poweredByTex])
}

// Register cleanup tasks:

clean.dependsOn('cleanTextures')

tasks.register('cleanTextures', Delete) {
    delete(poweredByTex)
    description 'Deletes generated texture assets.'
}
