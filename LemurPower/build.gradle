// Note: "common.gradle" in the root project contains additional initialization
//   for this project. This initialization is applied in the "build.gradle"
//   of the root project.

plugins {
    id 'java-library'
    id 'maven-publish'
}

description = 'a startup screen for jMonkeyEngine + Lemur'
ext {
    artifact = 'LemurPower'
    metadata = jmeTarget

    if (metadata.equals('')) {
        version = lemurPowerVersion
    } else {
        version = "${lemurPowerVersion}+${metadata}"
    }
    baseName = "${artifact}-${version}"
}

jar { archiveBaseName = project.ext.baseName }
javadocJar { archiveBaseName = project.ext.baseName }
sourcesJar { archiveBaseName = project.ext.baseName }

dependencies {
    //api 'com.github.stephengold:JmePower:' + jmePowerVersion
    api project(':JmePowerLibrary')

    api 'com.simsilica:lemur:1.14.0'
}

build.dependsOn('pom')
task('pom', type: Copy) {
    dependsOn 'generatePomFileForMavenPublication'
    from "${buildDir}/publications/maven/pom-default.xml"
    into "${buildDir}/libs"
    rename 'pom-default.xml', baseName + '.pom'
}

publishToMavenLocal.doLast {
    println 'published to ' + baseName
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId artifact
            from components.java
            pom {
                description = project.description
                developers {
                    developer {
                        email = 'sgold@sonic.net'
                        name = 'Stephen Gold'
                    }
                }
                licenses {
                    license {
                        distribution = 'repo'
                        name = 'New BSD (3-clause) License'
                        url = 'https://opensource.org/licenses/BSD-3-Clause'
                    }
                }
                name = 'com.github.stephengold:' + artifact
                scm {
                    connection = 'scm:git:git://github.com/stephengold/JmePower.git'
                    developerConnection = 'scm:git:ssh://github.com:stephengold/JmePower.git'
                    url = 'https://github.com/stephengold/JmePower/tree/master'
                }
                url = 'https://github.com/stephengold/JmePower'
            }
            version project.ext.version
        }
    }
}
