// Gradle settings and tasks common to all subprojects

apply plugin: 'java'

group = 'com.github.stephengold'

sourceCompatibility = '1.7'
targetCompatibility = '1.7'

gradle.projectsEvaluated {
    tasks.withType(JavaCompile) { // Java compile-time options:
        options.compilerArgs << '-Xdiags:verbose'
        options.compilerArgs << '-Xlint:unchecked'
        options.deprecation = true
        options.encoding = 'UTF-8'
    }
    tasks.withType(JavaExec) { // Java runtime options:
        classpath sourceSets.main.runtimeClasspath
        //doFirst { println commandLine }
        enableAssertions true
        //jvmArgs '-verbose:gc'
        //jvmArgs '-Xms1024m', '-Xmx1024m'
        //jvmArgs '-XX:+UseConcMarkSweepGC'
        //jvmArgs '-XX:+UseG1GC', '-XX:MaxGCPauseMillis=10'
    }
}

ext {
    jmeTarget = ''

    // current versions of various libraries
    heartVersion = '7.0.0'
    jmonkeyengineVersion = '3.4.0-stable'
    jmePowerVersion = '0.4.4-SNAPSHOT'
    lemurPowerVersion = '0.4.4-SNAPSHOT'
}

repositories {
    //mavenLocal()
    mavenCentral()
    maven { url 'https://jcenter.bintray.com/' } // for Lemur
}

// custom tasks for creating source/javadoc JARs
task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    dependsOn 'classes'
    description 'Create a JAR of source files.'
    from sourceSets.main.allJava
}
task javadocJar(type: Jar) {
    archiveClassifier = 'javadoc'
    dependsOn 'javadoc'
    description 'Create a JAR of javadoc.'
    from javadoc.destinationDir
}
