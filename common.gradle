// Gradle settings and tasks common to all JmePower subprojects

apply plugin: 'java'

ext {
    jmeTarget = ''
    group = 'com.github.stephengold'
    websiteUrl = 'https://github.com/stephengold/JmePower'

    // current versions of various libraries:
    heartVersion = '7.1.0'
    jmonkeyengineVersion = '3.4.1-stable'
    jmePowerVersion = '0.4.4-SNAPSHOT'
    lemurPowerVersion = '0.4.4-SNAPSHOT'
}

sourceCompatibility = '1.7'
targetCompatibility = '1.7'

tasks.withType(JavaCompile) { // Java compile-time options:
    options.compilerArgs << '-Xdiags:verbose'
    options.compilerArgs << '-Xlint:unchecked'
    options.deprecation = true
    options.encoding = 'UTF-8'
}

tasks.withType(JavaExec) { // Java runtime options:
    classpath sourceSets.main.runtimeClasspath
    enableAssertions true
    //jvmArgs '-verbose:gc'
    //jvmArgs '-Xms1024m', '-Xmx1024m'
    //jvmArgs '-XX:+UseConcMarkSweepGC'
    //jvmArgs '-XX:+UseG1GC', '-XX:MaxGCPauseMillis=10'
}

repositories {
    //mavenLocal()
    mavenCentral()
    maven { url 'https://jcenter.bintray.com/' } // for Lemur
}

// custom tasks for creating source/javadoc JARs

task javadocJar(type: Jar) {
    archiveClassifier = 'javadoc'
    dependsOn 'javadoc'
    description 'Creates a JAR of javadoc.'
    from javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    description 'Creates a JAR of sourcecode.'
    from sourceSets.main.allJava
}
