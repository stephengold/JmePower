// Gradle settings and tasks common to all JmePower subprojects

apply plugin: 'java'

ext {
    jmeTarget = '' // distinguish libraries built for different JME releases
    group = 'com.github.stephengold'
    websiteUrl = 'https://github.com/stephengold/JmePower'

    // module coordinates of dependencies:
    heartCoordinates = 'com.github.stephengold:Heart:7.2.0' + jmeTarget

    // current versions of libraries:
    jme3Version = '3.5.0-stable'
    jmePowerVersion = '0.4.4-SNAPSHOT'
    lemurPowerVersion = '0.4.4-SNAPSHOT'
}

sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_8

tasks.withType(JavaCompile) { // Java compile-time options:
    options.compilerArgs << '-Xdiags:verbose'
    options.compilerArgs << '-Xlint:unchecked'
    options.deprecation = true
    options.encoding = 'UTF-8'
}

tasks.withType(JavaExec) { // Java runtime options:
    classpath sourceSets.main.runtimeClasspath
    enableAssertions true
    //jvmArgs '-verbose:gc'
    //jvmArgs '-Xms1024m', '-Xmx1024m'
    //jvmArgs '-XX:+UseConcMarkSweepGC'
    //jvmArgs '-XX:+UseG1GC', '-XX:MaxGCPauseMillis=10'
}

repositories {
    //mavenLocal()
    mavenCentral()
    maven { url 'https://jcenter.bintray.com' } // for Lemur
}

// custom tasks for creating source/javadoc JARs

task javadocJar(type: Jar) {
    archiveClassifier = 'javadoc'
    dependsOn 'javadoc'
    description 'Creates a JAR of javadoc.'
    from javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    description 'Creates a JAR of sourcecode.'
    from sourceSets.main.allJava
}
