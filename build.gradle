// Gradle script to build the JmePower project

plugins {
    id 'base' // to add a "clean" task to the root project
}

ext {
    jmeTarget = '' // distinguish non-JME libraries built for specific JME releases

    // module coordinates of external dependencies:
    heartCoordinates = 'com.github.stephengold:Heart:9.0.0' + jmeTarget

    // current versions of libraries:
    jme3Version = '3.7.0-beta1'
    jmePowerVersion = '1.1.2-SNAPSHOT'
}

subprojects {
    apply from: rootProject.file('common.gradle')
}

tasks.register('checkstyle') {
    dependsOn ':JmePowerAssets:checkstyleMain', \
        ':JmePowerExamples:checkstyleMain', \
        ':JmePowerLibrary:checkstyleMain'
    description 'Checks the style of all Java sourcecode.'
}

tasks.register('run') {
    dependsOn ':JmePowerExamples:run'
    description 'Runs the examples.'
}

// Register publishing tasks:

tasks.register('install') {
    dependsOn ':JmePowerLibrary:install'
    description 'Installs Maven artifacts to the local repository.'
}
tasks.register('release') {
    dependsOn ':JmePowerLibrary:release'
    description 'Stages Maven artifacts to Sonatype OSSRH.'
}
